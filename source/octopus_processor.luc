module octopus_processor (
    input clk,  // clock
    input rst,  // reset
    input left,
    input right,
    input reset,
    
    output score[16],
    output pc_pos[16],
    output tent_1_len[16],
    output tent_2_len[16],
    output tent_3_len[16],
    output tent_4_len[16],
    
    // debug sig
    output deb_asel[16],
    output deb_bsel[16],
    output deb_alu[16],
    output deb_wsel[16]
  )

{
  st_alu alu;
  sig asel[16];
  sig bsel[16];
  sig wsel[16];
  
  .clk(clk) {
    // edge detectors for all control unit inputs
    edge_detector left_button(#RISE(1), #FALL(0));
    edge_detector right_button(#RISE(1), #FALL(0));
    edge_detector tick_edge(#RISE(1), #FALL(0));
    
    .rst(rst) {
      regfile regfile;
      ctrl_unit ctrl_unit;
      pn_gen xorshift_rng;
      counter tick(#SIZE(1),#DIV(25)); // change #DIV to adjust speed of octopus
    }
  }
  always {
    // connect control unit inputs to edge detectors
    left_button.in = left;
    right_button.in = right;
    tick_edge.in = tick.value;
    
    ctrl_unit.left = left_button.out;
    ctrl_unit.right = right_button.out;
    ctrl_unit.tick = tick_edge.out;
    ctrl_unit.reset = reset;
    ctrl_unit.b_data = regfile.b_data;
    
    // control unit regfile connections
    regfile.we = ctrl_unit.we;
    regfile.ra = ctrl_unit.ra;
    regfile.rb = ctrl_unit.rb;
    regfile.wr = ctrl_unit.wr;
    
    // alu connections, asel and bsel muxes
    alu.alufn = ctrl_unit.alufn;
    
    case (ctrl_unit.asel) {
      b00 : asel = regfile.a_data;
      b01 : asel = hC000;
      b10 : asel = 0;
      b11 : asel = hFFFF;
      default :
        asel = regfile.a_data;
    }
    
    alu.a = asel;
    deb_asel = asel;
    
    case (ctrl_unit.bsel) {
      b00 : bsel = regfile.b_data;
      b01 : bsel = h0001;
      b10 : bsel = 4;
      b11 : bsel = 8;
      default :
        bsel = regfile.b_data;
    }
    
    alu.b = bsel;
    deb_bsel = bsel;
    
    // wsel connections
    xorshift_rng.next = ctrl_unit.prng;
    xorshift_rng.seed = 5;
    
    case (ctrl_unit.wsel) {
      b0 : wsel = alu.out;
      b1 : wsel = xorshift_rng.num[15:0];
      default :
        wsel = alu.out;
    }
    
    // various debug signals
    deb_alu = alu.out;
    regfile.write_data = wsel;
    deb_wsel = wsel;
    
    // regfile outputs
    score = regfile.score;
    pc_pos = regfile.pc_pos;
    tent_1_len = regfile.tent_1_len;
    tent_2_len = regfile.tent_2_len;
    tent_3_len = regfile.tent_3_len;
    tent_4_len = regfile.tent_4_len;
    
  }
}
